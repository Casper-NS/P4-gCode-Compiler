const float layerHeight = 0.2
const float lineWidth = 0.4
const int wallLayers = 20
const float size = 5
const int iterations = 5

const vector cornerPoint = (100, 100, 0)

float stepLength

void main()
{
    setup()
    int layer = 0
    walk AbsMove(cornerPoint)

	stepLength = size/iterations
    repeat(wallLayers){
	    build A(iterations)
        walk Lift(layerHeight)
    }
}

void A(int iteration)
{
	if (iteration > 0)
	{
		B(iteration - 1)
		Right(60)
		A(iteration - 1)
		Right(60)
		B(iteration - 1)
	} else 
	{
		Steps(stepLength)
	}
}

void B(int iteration)
{
	if (iteration > 0)
	{
		A(iteration - 1)
		Left(60)
		B(iteration - 1)
		Left(60)
		A(iteration - 1)
	} else
	{
		Steps(stepLength)
	}
}

void setup()
{
    SetBedTemp(60)
    WaitForBedTemp()
    SetExtruderTemp(210)
    WaitForExtruderTemp()

    /* Setup speeds */
    gcode{
        M201 X500.00 Y500.00 Z100.00 E5000.00 ;Setup machine max acceleration
        M203 X500.00 Y500.00 Z10.00 E50.00 ;Setup machine max feedrate
        M204 P500.00 R1000.00 T500.00 ;Setup Print/Retract/Travel acceleration
        M205 X8.00 Y8.00 Z0.40 E5.00 ;Setup Jerk
        M220 S100 ;Reset Feedrate
        M221 S100 ;Reset Flowrate

    }

    Home()
    setupline()
    SetExtrusionRate(0.02)

}

void setupline()
{
    gcode{
        G1 Z2.0 F3000 ;Move Z Axis up
        G1 X10.1 Y20 Z0.28 F5000.0 ;Move to start position
        G1 X10.1 Y200.0 Z0.28 F1500.0 E15 ;Draw the first line
        G1 X10.4 Y200.0 Z0.28 F5000.0 ;Move to side a little
        G1 X10.4 Y20 Z0.28 F1500.0 E30 ;Draw the second line
        G92 E0 ;Reset Extruder
        G1 Z2.0 F3000 ;Move Z Axis up
        G92 E0 ;Reset Extruder  

    }

}
